      subroutine simplex(pmin,p,y,lamda)

c     This file is part of StarFISH
c     (C) 2004 by Jason Harris  <jharris@as.arizona.edu>
c     
c -=> StarFISH is free software; you can redistribute it 
c -=> and/or modify it under the terms of the GNU General Public 
c -=> License (GPL) as published by the Free Software Foundation;
c -=> either version 2 of the License, or (at your option) any 
c -=> later version.
c
C     simplex: construct the parameter-space simplex used to determine the 
C     local fitstat gradient.  The simplex is a collection of MP+1 locations
C     in parameter space.  Starting at a canonical location, the other MP 
C     locations are generated by perturbing the canonical location by a small 
C     constant amount in each parameter dimension.
C
      include 'sfh.h'

      real lamda
      double precision p(MP+1,MP),pmin(MP),x(MP),xran(105),amp
      double precision y(MP+1),yp
      integer i,j,np

      common /rnd/ xran
      common /cnp/ np

      do j=1,np
         p(1,j) = pmin(j)
      end do
      call fitstat(pmin,y(1),2)

      do i=2,np+1
         do j=1,np
            if (i-1.eq.j) then
               if (p(1,j).lt.dble(1.0D0-lamda)) then
                  p(i,j) = p(1,j) + dble(lamda)
               else
                  p(i,j) = p(1,j) - dble(lamda)
               endif
            else
               p(i,j) = p(1,j)
            endif
            if (p(i,j).lt.0.D0) p(i,j) = 0.0001D0
            x(j) = p(i,j)
         end do
         
         call fitstat(x,y(i),0)
      end do

      return
      end








